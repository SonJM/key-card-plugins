plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'org.blisle'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "tr7zw-repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    implementation group: 'net.kyori', name: 'adventure-api', version: '4.17.0'
    implementation group: 'net.kyori', name: 'adventure-platform-bukkit', version: '4.3.3'
    implementation 'org.xerial:sqlite-jdbc:3.41.2.2'
    implementation "de.tr7zw:item-nbt-api:2.12.4"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    shadowJar {
        relocate 'de.tr7zw.changeme.nbtapi', 'org.blisle.hxxniverskeycard.nbtapi'
        archiveClassifier.set('')
        archiveFileName.set('hxxnivers_key_card.jar')
        destinationDirectory.set(file('C:\\Users\\wingk\\Desktop\\server-buckit\\plugins'))
    }

    jar {
        enabled = false // 기본 jar 태스크를 사용하지 않도록 설정
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}
